EXPORT NYQUIST(a,b,c,d,e)
BEGIN
  LOCAL MIDDLEX,MIDDLEY,NUMER,DENOM,MAGN,RES,PREC,MAXW;

  RECT();

  MIDDLEX:=127;
  MIDDLEY:=62;
  NUMER:=a;
  DENOM:=b;
  MAGN:=c;
  PREC:=d;
  MAXW:=e;

  //Draw axis lines
  LINE_P(0,MIDDLEY,254,MIDDLEY,2);
  LINE_P(MIDDLEX,0,MIDDLEX,125,2);

  //Draw ticks
  FOR T FROM -10 TO 10 STEP 0.1 DO
    RES:=MIDDLEX-T*MAGN;
    LINE_P(RES,MIDDLEY+1,RES,MIDDLEY-1,2);
    RES:=MIDDLEY-T*MAGN;
    LINE_P(MIDDLEX+1,RES,MIDDLEX-1,RES,2);
  END;

  FOR W FROM -MAXW TO MAXW STEP PREC DO
    RES:=POLYEVAL(DENOM, W*i);
    IF RES==0 THEN
      RES:=0;
    ELSE
      RES:=MAGN*POLYEVAL(NUMER, W*i)/RES;
    END;
    PIXON_P(INT(RE(RES))+MIDDLEX, INT(IM(RES))+MIDDLEY);
  END;

  //Draw tick at -1
  RES:=MIDDLEX-1*MAGN;
  LINE_P(RES-1,MIDDLEY+3,RES-1,MIDDLEY-3,1);
  LINE_P(RES,MIDDLEY+4,RES,MIDDLEY-4,1);
  LINE_P(RES+1,MIDDLEY+3,RES+1,MIDDLEY-3,1);
  TEXTOUT_P("FINISHED",0,0,1);
  WAIT(0);

END;
